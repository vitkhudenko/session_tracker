plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
    id 'org.jetbrains.kotlinx.kover' version "$kover_version"
}

def libraryVersionName = '2.0.0'
def artifactName = 'SessionTracker'

android {

    buildFeatures {
        buildConfig = false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 31
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            if (name == "testReleaseUnitTest") {
                kover {
                    enabled = true
                }
            }
        }
    }
}

tasks.koverHtmlReport {
    enabled = true
    htmlReportDir.set(file("/reports/kover"))
}

tasks.koverXmlReport {
    enabled = false
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

task cleanReports(type: Delete) {
    delete file("/reports/")
}

tasks.clean.dependsOn(tasks.cleanReports)

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.github.vitkhudenko:state_machine:0.3.8'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.2.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'io.mockk:mockk:1.12.1'
    //noinspection GradleDependency
    testImplementation 'org.json:json:20211205'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                from components.release

                groupId 'com.github.vitkhudenko'
                artifactId 'session_tracker'
                version libraryVersionName

                artifacts {
                    artifact sourcesJar {
                        classifier 'sources'
                    }
                }

                pom {
                    name = artifactName
                    description = 'A general purpose framework to provide a foundation for session management.'
                    url = 'https://github.com/vitkhudenko/session_tracker'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/vitkhudenko/session_tracker/blob/master/LICENSE'
                        }
                    }
                }

            }
        }
    }
}
